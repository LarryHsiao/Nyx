buildscript {
    ext {
        camerax_version = "1.1.0-alpha05"
        kotlinVersion = '1.4.10'
        lifecycle_version = "2.2.0"
        nav_version = "2.3.5"
    }
    repositories {
        maven {url = "https://larryhsiao.com:11000/repository/maven-public/"}
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath "com.github.triplet.gradle:play-publisher:3.4.0"
        classpath 'com.google.gms:google-services:4.3.8'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.7.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
    }
}
apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.larryhsiao.nyx"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode nyxVersionCode
        versionName nyxVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def AUTHORITY = "com.larryhsiao.nyx.fileprovider"
        def ATTACHMENT_FOLDER_NAME = "attachments"
        buildConfigField("String", "URI_HOST", "\"https://larryhsiao.com\"")
        buildConfigField("String", "ATTACHMENT_FOLDER_NAME", "\"$ATTACHMENT_FOLDER_NAME\"")
        buildConfigField("String", "FILE_PROVIDER_AUTHORITY", "\"$AUTHORITY\"")
        buildConfigField("String", "URI_FILE_PROVIDER", "\"content://$AUTHORITY/$ATTACHMENT_FOLDER_NAME/\"")
        buildConfigField("String", "OPEN_WEATHER_API_KEY", "\"a5817f5f724548529f05f64e8780ee41\"")
        buildConfigField("int", "JPG_COMPRESS_QUALITY", "85")
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8

        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    packagingOptions {
    }

    lintOptions {
        lintConfig file("lint.xml")
    }

    sourceSets {
        main { assets.srcDirs += "../core/src/main/resources" }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }

    buildFeatures {
        viewBinding = true
    }
}
configurations.all {
    resolutionStrategy.force 'junit:junit:4.13'
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation project(':nyx-core')
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'
    implementation 'com.google.guava:guava:27.0.1-android'
    implementation "androidx.palette:palette-ktx:1.0.0"
    implementation "androidx.fragment:fragment-ktx:1.3.4"
    implementation "androidx.navigation:navigation-runtime-ktx:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.appcompat:appcompat:1.3.0"
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"
    implementation "androidx.camera:camera-view:1.0.0-alpha25"
    implementation "androidx.camera:camera-extensions:1.0.0-alpha25"
    implementation "androidx.biometric:biometric:1.1.0"
    implementation "androidx.core:core-ktx:1.3.1"
    implementation 'org.jasypt:jasypt:1.9.3'
    implementation 'jp.wasabeef:picasso-transformations:2.2.1'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.android.billingclient:billing:3.0.0'
    implementation group: 'com.linkedin.urls', name: 'url-detector', version: '0.1.17'
    implementation 'io.github.ponnamkarthik:richlinkpreview:1.0.9'
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.13.1'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.larryhsiao:aura:1.24.2'
    implementation platform('com.google.firebase:firebase-bom:28.1.0')
    implementation 'com.firebaseui:firebase-ui-auth:6.4.0'
    implementation 'com.google.firebase:firebase-iid'
    implementation 'com.google.firebase:firebase-storage'
    implementation 'com.google.firebase:firebase-firestore'
    implementation 'com.google.firebase:firebase-config'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.haibin:calendarview:3.6.8'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    implementation 'com.github.stfalcon:stfalcon-imageviewer:1.0.0'
    implementation 'com.google.maps.android:maps-utils-ktx:1.8.0'
    implementation 'com.google.android.material:material:1.4.0-rc01'
    implementation 'com.schibstedspain.android:leku:6.4.0'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation 'com.google.http-client:google-http-client-gson:1.39.2'
    implementation('com.google.api-client:google-api-client-android:1.26.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation('com.google.apis:google-api-services-drive:v3-rev136-1.25.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.robolectric:robolectric:4.3.1"
    androidTestImplementation "androidx.test:runner:1.3.0"
}

apply from: 'deploy.gradle'
